grammar org.mondo.collaboration.security.mpbl.xtext.MondoPropertyBasedLocking with org.eclipse.xtext.common.Terminals

generate mondoPropertyBasedLocking "http://www.mondo.org/collaboration/security/mpbl/xtext/MondoPropertyBasedLocking"
import "http://www.eclipse.org/incquery/patternlanguage/PatternLanguage" as iql
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

PropertyBasedLockingModel:
	{PropertyBasedLockingModel}
	roles+=Role*
	'locks' '{' locks+=Lock* '}'
;

Role:
	User | Group
;

User:
	'user' name=ID
;

Group:
	'group' name=ID '{'  users+=[User] (',' users+=[User])* '}'
;	

	
Lock:
	owner=[Role] 'lock' '{' 'query' pattern=[iql::Pattern|STRING] (bindings+=Binding)* '}'
;

Binding:
	'bind' param=[iql::Variable] value=Bind
;

Bind:
	ObjectBind | ValueBind | RoleBind
;

RoleBind:
	'role' role=[Role]
;

ValueBind:
	'value' value=STRING
;

ObjectBind:
	'object' object=[ecore::EObject]
;
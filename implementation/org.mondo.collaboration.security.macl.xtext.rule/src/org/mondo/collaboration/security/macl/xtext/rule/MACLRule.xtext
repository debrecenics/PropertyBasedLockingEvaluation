grammar org.mondo.collaboration.security.macl.xtext.rule.MACLRule with org.eclipse.xtext.common.Terminals

generate mACLRule "http://www.mondo.org/collaboration/security/macl/xtext/rule/MACLRule"

import "http://www.eclipse.org/incquery/patternlanguage/PatternLanguage" as iql
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

RuleModel:
	roles+=Role*
	rules+=Rule*
;
	
Role:
	User | Group
;

User:
	'user' name=ID
;

Group:
	'group' name=ID '{'  users+=[User] (',' users+=[User])* '}'
;	
	
Rule:
	'rule' name=ID contraint=RuleConstraint 'to' roles+=[Role](',' roles+=[Role])*
	'{'		
		'query' pattern=[iql::Pattern|STRING] ';'?
		bindings+=Binding*
		
	'}'
;

Binding:
	'bind' param=[iql::Variable] value=Bind
;

Bind:
	ObjectBind | ValueBind | RoleBind
;

RoleBind:
	'role' role=[Role]
;

ValueBind:
	'value' value=STRING
;

ObjectBind:
	'object' object=[ecore::EObject]
;

RuleConstraint:
	type=RuleType rights=RuleRights | 'obfuscate'
;

enum RuleRights:
	Read = 'R' | Write = 'W' | ReadWrite = 'RW'
;

enum RuleType:
	Deny = 'deny' | Permit = 'permit'
;
